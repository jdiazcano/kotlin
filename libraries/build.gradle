
buildscript {
    ext.kotlin_version = "1.1-SNAPSHOT"
    repositories {
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1-M04"
    }
}

allprojects  {
    apply plugin: 'maven'

    group = 'org.jetbrains.kotlin'
    version = "$kotlin_version"
}

subprojects {
    apply plugin: 'kotlin'

    jar {
        baseName = project.name
        manifest {
            attributes 'Implementation-Vendor': 'JetBrains',
                    'Implementation-Title': "${project.description ?: project.name}", // TODO: project.description is resolved after evaluating this, therefore this attribute is repeated in the projects; seek for a solution
                    'Implementation-Version': version,
                    'Build-Jdk': System.getProperty('java.version'),
//                    'Kotlin-Version': kotlin.language.version, // TODO: if we need it - find the way to extract it
                    'Built-By': 'JetBrains'
        }
        from("${rootDir}/../dist/kotlinc/build.txt") { into("META-INF/") }
    }

    // TODO: use dokka instead?
    javadoc {
        failOnError = false
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    compileKotlin.compilerJarFile = project.file("${rootDir}/../dist/kotlinc/lib/kotlin-compiler.jar")
    compileTestKotlin.compilerJarFile = project.file("${rootDir}/../dist/kotlinc/lib/kotlin-compiler.jar")
}
