
buildscript {
    ext.kotlin_version = "1.1-SNAPSHOT"
    repositories {
        flatDir {
            dirs 'tools/kotlin-gradle-plugin/target',
                 'tools/kotlin-gradle-plugin/target/dependency'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects  {
    apply plugin: 'maven'

    group = 'org.jetbrains.kotlin'
    version = "$kotlin_version"
}

subprojects {
    apply plugin: 'kotlin'

    jar {
        baseName = project.name
        manifest {
            attributes 'Implementation-Vendor': 'JetBrains',
                    'Implementation-Title': "${project.description ?: project.name}", // TODO: project.description is resolved after evaluating this, therefore this attribute is repeated in the projects; seek for a solution
                    'Implementation-Version': version,
                    'Build-Jdk': System.getProperty('java.version'),
//                    'Kotlin-Version': kotlin.language.version, // TODO: if we need it - find the way to extract it
                    'Built-By': 'JetBrains'
        }
        from("${rootDir}/../dist/kotlinc/build.txt") { into("META-INF/") }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
}
