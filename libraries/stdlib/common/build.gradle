description = 'Kotlin Common Standard Library'

apply plugin: 'kotlin-platform-common'

ext.commonSrcDir = "${buildDir}/common-sources"

sourceSets {
    main {
        kotlin {
            srcDir 'src'
            srcDir commonSrcDir
        }
    }
}

ant.properties.baseDir = projectDir
ant.properties.commonSrcDir = commonSrcDir
ant.properties.kotlinCompiler = bootstrapCompilerFile.path
ant.properties.compilerPreloader = compilerPreloaderFile.path

ant.importBuild('tasks.xml')

compileKotlinCommon.dependsOn commonSources

/*
// TODO: currently unsupported
compileKotlinCommon {
    kotlinOptions {
        freeCompilerArgs = [
                "-module-name", "${project.name}".toString()
        ]
    }
}
*/

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Vendor': 'JetBrains',
                'Implementation-Title': "${project.description ?: project.name}", // TODO: project.description is resolved after evaluating this, therefore this attribute is repeated in the projects; seek for a solution
                'Implementation-Version': version,
                'Build-Jdk': System.getProperty('java.version'),
                'Built-By': 'JetBrains'
    }
    from("${rootDir}/../dist/kotlinc/build.txt") { into("META-INF/") }
}


// TODO: use dokka instead?
javadoc {
    failOnError = false
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.kotlin

}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

