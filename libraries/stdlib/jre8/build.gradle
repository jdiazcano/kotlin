description = 'Kotlin Standard Library JRE 8 extension'

dependencies {
    compile project(':kotlin-stdlib')
    compile project(':kotlin-stdlib-jre7')
    testCompile project(':kotlin-test:kotlin-test-junit')
}

sourceSets {
    main {
        kotlin {
            srcDir 'src'
        }
    }
    test {
        kotlin {
            srcDir 'test'
            if(!System.properties.'idea.active') {
                srcDir '../test'
                srcDir '../jre7/test'
            }
        }
    }
}

jar {
    manifestAttributes(manifest, project, 'Core')
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

compileKotlin {
    kotlinOptions.jdkHome = JDK_18
    kotlinOptions.jvmTarget = 1.8
    kotlinOptions.freeCompilerArgs = [
            "-Xallow-kotlin-package",
            "-Xmultifile-parts-inherit",
            "-Xdump-declarations-to", "${buildDir}/stdlib-jre8-declarations.json".toString(),
            "-module-name", "${project.name}".toString()
    ]
}

compileTestKotlin {
    kotlinOptions.jdkHome = JDK_18
    kotlinOptions.jvmTarget = 1.8
    kotlinOptions.freeCompilerArgs = ["-Xallow-kotlin-package"]
}

kotlin.experimental.coroutines 'enable'

test {
    executable = "$JDK_18/bin/java"
}


task testJre6Tests(type: Test) {
    dependsOn project(':kotlin-stdlib').tasks.testClasses
    group = "verification"

    executable = "$JDK_18/bin/java"

    testClassesDir = project.file('../build/classes/test')
    classpath = project.files('../build/classes/test') + sourceSets.test.compileClasspath
}

check.dependsOn testJre6Tests